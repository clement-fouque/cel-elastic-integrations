request(
    "GET",
    (has(state.cursor) && has(state.cursor.next_url) && state.cursor.next_url != null) ?
        state.cursor.next_url
    :
        state.url + "?" + {
            "per_page": [state.batch_size],
            "type": ["reviewed"],
        }.format_query()
).with(
    {
        "Header": {
            "Accept": ["application/vnd.github+json"],
            "X-GitHub-Api-Version": ["2022-11-28"],
            "Authorization": ["Bearer " + token],
        },
    }
).do_request().as(resp,
    bytes(resp.Body).decode_json().as(body,
        {
            "events": body.map(
                e,
                {
                        "message": e.encode_json(),
                }
            ),
            "url": state.url,
            "want_more": resp.Header.Link[0].re_match('github_link_next'),
            "batch_size": state.batch_size,
            "token": token,
            "cursor": {
                "next_url": resp.Header.Link[0].re_find('github_link_next').trim_prefix('<').split('>')[0],
            },
        }
    )
)